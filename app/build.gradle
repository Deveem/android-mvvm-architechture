plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'androidx.navigation.safeargs.kotlin'
    id 'kotlin-kapt'
    id 'io.github.rockerhieu.versionberg' version '1.0.2'
}

apply from: "distribution.gradle"

versionberg {
    major 1 //Increase when you make incompatible API changes (default value is 0)
    minor 0 //Increase when you add functionality in a backwards-compatible manner (default value is 0)
    patch 0 //Increase when you make backwards-compatible bug fixes (default value is 0)
    // build 2 default is ${commitCount}, uncomment to use a custom build number
    nameTemplate = "$major.$minor.$commitCount"
    codeTemplate = "$build"
}

android {

    compileSdk 33

    project.ext {
        koin_version = "2.2.3"
        retrofit_version = '2.9.0'
        moshi_version = '1.12.0'
    }

    defaultConfig {
        applicationId "io.deveem.mvvm.android"
        minSdk 22
        targetSdk 33
        versionCode versionberg.code
        versionName versionberg.name

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false

            buildConfigField "String", "BASE_URL", "\"http://jsonplaceholder.typicode.com\""
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

        debug {
            debuggable true

            buildConfigField "String", "BASE_URL", "\"http://jsonplaceholder.typicode.com\""
            minifyEnabled false
        }
    }

    // Generated apk/aab file name
    android.applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "$applicationId-${variant.name}-${variant.versionCode}(v${variant.versionName}).apk"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        viewBinding true
    }
}

dependencies {

    // UI
    implementation 'com.squareup.picasso:picasso:2.71828'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.navigation:navigation-fragment-ktx:2.5.3'
    implementation 'androidx.navigation:navigation-ui-ktx:2.5.3'

    // Network
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-moshi:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"
    implementation "com.squareup.okhttp3:logging-interceptor:5.0.0-alpha.2"

    implementation "com.squareup.moshi:moshi-kotlin:$moshi_version"
    implementation "com.squareup.moshi:moshi:$moshi_version"

    // KoinDI
    implementation "io.insert-koin:koin-android:$koin_version"
    implementation "io.insert-koin:koin-androidx-scope:$koin_version"
    implementation "io.insert-koin:koin-androidx-viewmodel:$koin_version"
    implementation "io.insert-koin:koin-androidx-fragment:$koin_version"

    // Architecture Components
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.6.1'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.6.1'

    // Logs
    implementation 'com.jakewharton.timber:timber:4.7.1'
    debugImplementation 'com.readystatesoftware.chuck:library:1.1.0'
    releaseImplementation 'com.readystatesoftware.chuck:library-no-op:1.1.0'

    // Core
    implementation 'androidx.core:core-ktx:1.10.1'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.6.0'

    // Unit tests
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}